
function(CGET_ADD_CUSTOM_TARGET name)
    ADD_CUSTOM_TARGET(${name} ${ARGN})
    set_property(TARGET ${name} PROPERTY FOLDER "CGet targets")
endfunction()

function(CGET_MESSAGE LVL)
    if (NOT CGET_VERBOSE_LEVEL LESS LVL)
        message("cget(${CGET_CURRENT_CHAIN}): ${ARGN}")
    endif ()
endfunction()

macro(CGET_EXECUTE_PROCESS)
    CGET_MESSAGE(3 "Running exec process: ${ARGN}")
    execute_process(${ARGN} RESULT_VARIABLE EXECUTE_RESULT ERROR_VARIABLE ERROR_RESULT)
    if (EXECUTE_RESULT)
        message(FATAL_ERROR "Execute process '${ARGN}' failed with '${EXECUTE_RESULT}', result: ${RESULT_VARIABLE} ${ERROR_RESULT}")
    endif ()
endmacro()

macro(CGET_GLOB_MULTIDIR var paths exts)
    set(${var})
    set(allpaths)
    foreach (path ${paths})
        foreach (ext ${exts})
            list(APPEND allpaths "${path}/*.${ext}")
        endforeach ()
    endforeach ()
    file(GLOB_RECURSE ${var} ${allpaths})
    CGET_MESSAGE(3 "CGET_GLOB_MULTIDIR ${${var}}")
endmacro()

# Stolen from http://stackoverflow.com/questions/4346412/how-to-prepend-all-filenames-on-the-list-with-common-path
FUNCTION(CGET_PREPEND_TO_LIST var prefix)
    SET(listVar "")
    FOREACH(f ${ARGN})
        LIST(APPEND listVar "${prefix}/${f}")
    ENDFOREACH(f)
    SET(${var} "${listVar}" PARENT_SCOPE)
ENDFUNCTION(CGET_PREPEND_TO_LIST)

FUNCTION(CGET_ALL_FILENAME_COMPONENTS VAR FileNames COMP)
    set(RTN "")
    foreach(FileName ${FileNames})
        GET_FILENAME_COMPONENT(NAME "${FileName}" ${COMP})
        list(APPEND RTN ${NAME})
    endforeach()
    set(${VAR} ${RTN} PARENT_SCOPE)
ENDFUNCTION()


macro(CGET_PARSE_VERSION NAME INPUT RESULT)
    SET(${RESULT} ${${INPUT}})
    STRING(TOLOWER "${${RESULT}}" ${RESULT})
    STRING(TOLOWER ${NAME} CGET_${NAME}_LOWER)
    STRING(REPLACE "${CGET_${NAME}_LOWER}" "" ${RESULT} "${${RESULT}}")
    STRING(REGEX MATCH "([0-9]+[\\._]?)+" ${RESULT} "${${RESULT}}")
    STRING(REPLACE "_" "." ${RESULT} "${${RESULT}}")
endmacro()
